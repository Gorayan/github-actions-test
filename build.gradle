plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
}

repositories {
    mavenCentral()
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "16"
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    embed 'org.jetbrains.kotlin:kotlin-stdlib:1.5.30'
    embed 'org.jetbrains.exposed:exposed-core:0.35.1'
    embed 'org.jetbrains.exposed:exposed-dao:0.35.1'
    embed 'org.jetbrains.exposed:exposed-jdbc:0.35.1'
    embed 'org.jetbrains.exposed:exposed-java-time:0.35.1'
    embed 'mysql:mysql-connector-java:8.0.26'
}

group 'net.gorayan.mc'
version '0.0'

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    archiveName("${project.name}.jar")
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

test {
    useJUnitPlatform ()
}


build {
    dependsOn(jar)
    doLast {
        delete fileTree(dir: "${projectDir}/server/plugins", include: "${project.name}.jar")
        copy {
            from jar
            into new File("${projectDir}/server/plugins")
        }
    }
}
